plugins {
    id 'com.android.application'
}

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.olyseus.i_seed.control"
        minSdk 21 // DJI SDK 4.16.1 requirement
        targetSdk 32
        versionCode 9
        versionName "9.0"
        setProperty("archivesBaseName", "drone-${versionName}")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            // On x86 devices that run Android API 23 or above, if the application is targeted with API 23 or
            // above, FFmpeg lib might lead to runtime crashes or warnings.
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
        multiDexEnabled true
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    bundle {
        storeArchive {
            enable = false
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        jniLibs {
            keepDebugSymbols += ['*/*/libdjivideo.so', '*/*/libSDKRelativeJNI.so', '*/*/libFlyForbid.so', '*/*/libduml_vision_bokeh.so', '*/*/libyuv2.so', '*/*/libGroudStation.so', '*/*/libFRCorkscrew.so', '*/*/libUpgradeVerify.so', '*/*/libFR.so', '*/*/libDJIFlySafeCore.so', '*/*/libdjifs_jni.so', '*/*/libsfjni.so', '*/*/libDJICommonJNI.so', '*/*/libDJICSDKCommon.so', '*/*/libDJIUpgradeCore.so', '*/*/libDJIUpgradeJNI.so']
        }
        resources {
            excludes += ['META-INF/rxjava.properties']
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'com.olyseus.i_seed.control'
}

dependencies {
    implementation('com.dji:dji-sdk:4.16.1', {
        /**
         * Uncomment the "library-anti-distortion" if your app does not need Anti Distortion for Mavic 2 Pro and Mavic 2 Zoom.
         * Uncomment the "fly-safe-database" if you need database for release, or we will download it when DJISDKManager.getInstance().registerApp
         * is called.
         * Both will greatly reducing the size of the APK.
         */
        exclude module: 'library-anti-distortion'
        //exclude module: 'fly-safe-database'
    })

    compileOnly 'com.dji:dji-sdk-provided:4.16.1'

    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'com.google.android.gms:play-services-maps:18.0.2'
    implementation 'com.google.android.gms:play-services-location:20.0.0'
    implementation "androidx.multidex:multidex:2.0.1"
    implementation 'com.google.protobuf:protobuf-java:3.21.2'
}
